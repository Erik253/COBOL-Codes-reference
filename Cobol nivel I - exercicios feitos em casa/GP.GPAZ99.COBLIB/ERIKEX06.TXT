      *--------------------------------------------------------------*
      * EXERCICIO PG:16 PARTE 4 DA APOSTILA
      * JOB: ERIKEX06 | COMPILADOR: ERIKCO06
      *--------------------------------------------------------------*
       IDENTIFICATION                      DIVISION.
      *--------------------------------------------------------------*
       PROGRAM-ID.                         ERIKEX06.
       AUTHOR.                             ERIK.
      *--------------------------------------------------------------*
       ENVIRONMENT                         DIVISION.
      *--------------------------------------------------------------*
       CONFIGURATION                       SECTION.
       SPECIAL-NAMES.
           DECIMAL-POINT IS COMMA.
       INPUT-OUTPUT                        SECTION.
       FILE-CONTROL.
           SELECT CADFUNC ASSIGN           TO UT-S-CADFUNC
           FILE STATUS IS FS-CADFUNC
           .
      *
           SELECT FOLHAPAG ASSIGN          TO UT-S-FOLHAPAG
           FILE STATUS IS FS-FOLHAPAG
           .
      *--------------------------------------------------------------*
       DATA                                DIVISION.
      *--------------------------------------------------------------*
       FILE                                SECTION.
       FD  CADFUNC
           LABEL RECORD IS STANDARD
           BLOCK CONTAINS 0 CHARACTERS
           RECORDING MODE IS F
           RECORD CONTAINS 43 CHARACTERS
           DATA RECORD IS REG-CADFUNC
           .
      *
       01  REG-CADFUNC.
           05 CODFUNC-C                    PIC X(05).
           05 NOMEFUNC-C                   PIC X(30).
           05 VALORHORA-C                  PIC 9(02)V99.
           05 QTDHRSTRAB-C                 PIC 9(04).
      *
       FD  FOLHAPAG
           LABEL RECORD IS STANDARD
           BLOCK CONTAINS 0 CHARACTERS
           RECORDING MODE IS F
           RECORD CONTAINS 43 CHARACTERS
           DATA RECORD IS REG-FOLHAPAG
           .
      *
       01  REG-FOLHAPAG.
           05 CODFUNC-F                    PIC X(5).
           05 NOMEFUNC-F                   PIC X(30).
           05 SALARIO-F                    PIC 9(06)V99.
      *
       WORKING-STORAGE                     SECTION.
       77  FS-CADFUNC                      PIC X(02).
       77  FS-FOLHAPAG                     PIC X(02).
       77  WS-FS                           PIC X(02).
      *
       77  WS-CTLIDOS                      PIC 9(02).
       77  WS-CTGRAV                       PIC 9(02).
       77  WS-MSG                          PIC X(60).
      *
       77  WS-SALARIO                      PIC 9(06)V99.
       77  WS-SOMASAL                      PIC 9(09)V99.
       77  WS-SOMASAL-AUX                  PIC ZZZ.ZZZ.ZZ9,99.
      *
       01  WS-REG-CADFUNC.
           05 WS-CODFUNC-C                    PIC X(05).
           05 WS-NOMEFUNC-C                   PIC X(30).
           05 WS-VALORHORA-C                  PIC 9(02)V99.
           05 WS-QTDHRSTRAB-C                 PIC 9(04).
      *
       01  WS-REG-FOLHAPAG.
           05 WS-CODFUNC-F                    PIC X(5).
           05 WS-NOMEFUNC-F                   PIC X(30).
           05 WS-SALARIO-F                    PIC 9(06)V99.
      *--------------------------------------------------------------*
       PROCEDURE                           DIVISION.
      *--------------------------------------------------------------*
       0000-ERIKEX06.
           PERFORM 1000-INICIALIZAR
           PERFORM 2000-PROCESSAR UNTIL FS-CADFUNC = "10"
           PERFORM 3000-TERMINO
           STOP RUN
           .
      *
       1000-INICIALIZAR.
           INITIALIZE WS-CTLIDOS
                      WS-CTGRAV
                      WS-SOMASAL
      *
           OPEN INPUT CADFUNC
           IF FS-CADFUNC NOT = ZEROS
              MOVE " ERRO DE ABERTURA DO ARQ CADFUNC " TO WS-MSG
              MOVE FS-CADFUNC              TO WS-FS
              GO                           TO 9999-ERRO
           END-IF
      *
           OPEN OUTPUT FOLHAPAG
           IF FS-FOLHAPAG NOT = ZEROS
              MOVE " ERRO DE ABERTURA DO ARQ FOLHAPAG " TO WS-MSG
              MOVE FS-FOLHAPAG             TO WS-FS
              GO                           TO 9999-ERRO
           END-IF
      *
           PERFORM 1500-LER-CADFUNC
           .
      *
       1500-LER-CADFUNC.
           READ CADFUNC INTO WS-REG-CADFUNC
           IF FS-CADFUNC = ZEROS
              ADD 1                        TO WS-CTLIDOS
           ELSE
              IF FS-CADFUNC NOT = "10"
                 MOVE " ERRO DE LEITURA DO CADFUNC " TO WS-MSG
                 MOVE FS-CADFUNC           TO WS-FS
                 GO                        TO 9999-ERRO
              END-IF
           END-IF
           .
      *
       2000-PROCESSAR.
           MOVE WS-CODFUNC-C               TO WS-CODFUNC-F
           MOVE WS-NOMEFUNC-C              TO WS-NOMEFUNC-F
           COMPUTE WS-SALARIO = WS-QTDHRSTRAB-C * WS-VALORHORA-C
           MOVE WS-SALARIO                 TO WS-SALARIO-F
           COMPUTE WS-SOMASAL = WS-SOMASAL + WS-SALARIO
           WRITE REG-FOLHAPAG              FROM WS-REG-FOLHAPAG
           IF FS-FOLHAPAG = ZEROS
              ADD 1                        TO WS-CTGRAV
           ELSE
              MOVE " ERRO DE GRAVACAO DO ARQ FOLHAPAG " TO WS-MSG
              MOVE FS-FOLHAPAG             TO WS-FS
              GO                           TO 9999-ERRO
           END-IF
      *
           PERFORM 1500-LER-CADFUNC
           .
      *
        3000-TERMINO.
            MOVE WS-SOMASAL                TO WS-SOMASAL-AUX
            DISPLAY " "
            DISPLAY " ***************************************** "
            DISPLAY " *         INFORMACOES GERAIS   "
            DISPLAY " * TOTAL DE REGISTROS LIDOS:...: " WS-CTLIDOS
            DISPLAY " * TOTAL DE REGISTROS GRAVADOS.: " WS-CTGRAV
            DISPLAY " * VALOR TOTAL PAGO............: " WS-SOMASAL-AUX
            DISPLAY " ***************************************** "
      *
            CLOSE CADFUNC
            IF FS-CADFUNC NOT = ZEROS
               MOVE " ERRO DE FECHAMENTO DO ARQ CADFUNC " TO WS-MSG
               MOVE FS-CADFUNC             TO WS-FS
               GO                          TO 9999-ERRO
            END-IF
      *
            CLOSE FOLHAPAG
            IF FS-FOLHAPAG NOT = ZEROS
               MOVE " ERRO DE FECHAMENTO DO ARQ FOLHAPAG " TO WS-MSG
               MOVE FS-FOLHAPAG            TO WS-FS
               GO                          TO 9999-ERRO
            END-IF
            .
      *
       9999-ERRO.
           DISPLAY " "
           DISPLAY " *************************** "
           DISPLAY "      ROTINA DE ERRO   "
           DISPLAY " MENSAGEM....: " WS-MSG
           DISPLAY " FILE STATUS.: " WS-FS
           DISPLAY " TERMINO ANORMAL DO PROGRAMA "
           DISPLAY " *************************** "
           STOP RUN
           .
