      *--------------------------------------------------------------*
      * EXERCICIO PG:20 PARTE 4 DA APOSTILA
      * JOB: ERIKEX07 | COMPILADOR: ERIKCO07
      *--------------------------------------------------------------*
       IDENTIFICATION                      DIVISION.
      *--------------------------------------------------------------*
       PROGRAM-ID.                         ERIKEX07.
       AUTHOR.                             ERIK.
      *--------------------------------------------------------------*
       ENVIRONMENT                         DIVISION.
      *--------------------------------------------------------------*
       CONFIGURATION                       SECTION.
       SPECIAL-NAMES.
           DECIMAL-POINT IS COMMA.
       INPUT-OUTPUT                        SECTION.
       FILE-CONTROL.
           SELECT SIMUEMP ASSIGN           TO UT-S-SIMUEMP
           FILE STATUS IS FS-SIMUEMP
           .
           SELECT EMPAPROV ASSIGN          TO UT-S-EMPAPROV
           FILE STATUS IS FS-EMPAPROV
           .
      *--------------------------------------------------------------*
       DATA                                DIVISION.
      *--------------------------------------------------------------*
       FILE                                SECTION.
       FD  SIMUEMP
           LABEL RECORD IS STANDARD
           BLOCK CONTAINS 0 CHARACTERS
           RECORDING MODE IS F
           RECORD CONTAINS 18 CHARACTERS
           DATA RECORD IS REG-SIMUEMP
           .
      *
       01  REG-SIMUEMP.
           05 NUM-SIMULACAO-S              PIC 9(04).
           05 VAL-EMPRESTIMO-S             PIC 9(06)V99.
           05 TX-JUROS-S                   PIC 9(02)V99.
           05 QTD-PARCELAS-S               PIC 9(02).
      *
       FD  EMPAPROV
           LABEL RECORD IS STANDARD
           BLOCK CONTAINS 0 CHARACTERS
           RECORDING MODE IS F
           RECORD CONTAINS 35 CHARACTERS
           DATA RECORD IS REG-EMPAPROV
           .
      *
       01  REG-EMPAPROV.
           05 NUM-SIMULACAO-E              PIC 9(04).
           05 VAL-EMPRESTIMO-E             PIC 9(06)V99.
           05 TX-JUROS-E                   PIC 9(02)V99.
           05 QTD-PARCELAS-E               PIC 9(02).
           05 VALOR-PARCELA-E              PIC 9(06)V99.
           05 TOTAL-EMP-E                  PIC 9(07)V99.
      *
       WORKING-STORAGE                     SECTION.
       77  FS-SIMUEMP                      PIC X(02).
       77  FS-EMPAPROV                     PIC X(02).
      *
       77  WS-CTLIDOS                      PIC 9(02).
       77  WS-CTGRAV                       PIC 9(02).
       77  WS-FS                           PIC X(02).
       77  WS-MSG                          PIC X(60).
       77  WS-VALOR-PARCELA                PIC 9(06)V99.
       77  WS-TOTAL-EMP                    PIC 9(07)V99.
      *
       01  WS-REG-SIMUEMP.
           05 WS-NUM-SIMULACAO-S           PIC 9(04).
           05 WS-VAL-EMPRESTIMO-S          PIC 9(06)V99.
           05 WS-TX-JUROS-S                PIC 9(02)V99.
           05 WS-QTD-PARCELAS-S            PIC 9(02).
      *
       01  WS-REG-EMPAPROV.
           05 WS-NUM-SIMULACAO-E           PIC 9(04).
           05 WS-VAL-EMPRESTIMO-E          PIC 9(06)V99.
           05 WS-TX-JUROS-E                PIC 9(02)V99.
           05 WS-QTD-PARCELAS-E            PIC 9(02).
           05 WS-VALOR-PARCELA-E           PIC 9(06)V99.
           05 WS-TOTAL-EMP-E               PIC 9(07)V99.
      *
      *  ------------- VARIAVEIS DE DATA --------------
       01  WS-DATA-SISTEMA.
           05 WS-ANO                       PIC 9(02).
           05 WS-MES                       PIC 9(02).
           05 WS-DIA                       PIC 9(02).
       01  WS-DATA-FORMATADA.
           05 WS-DIA                       PIC 9(02).
           05 FILLER                       PIC X(01) VALUE "/".
           05 WS-MES                       PIC 9(02).
           05 FILLER                       PIC X(03) VALUE "/20".
           05 WS-ANO                       PIC 9(02).
      *
       01  WS-DATA-JULIANA.
           05 WS-ANO                       PIC 9(02).
           05 WS-DIA                       PIC 9(03).
      *
       77  WS-DIA-SEMANA                   PIC 9(01).
      *
      *----------------VARIAVEIS DE TEMPO-----------------------
       01  WS-HORARIO-I.
           05 WS-HORA-I                    PIC 9(02).
           05 WS-MIN-I                     PIC 9(02).
           05 WS-SEG-I                     PIC 9(02).
           05 WS-CENT-I                    PIC 9(02).
      *
       01  WS-HORARIO-F.
           05 WS-HORA-F                    PIC 9(02).
           05 WS-MIN-F                     PIC 9(02).
           05 WS-SEG-F                     PIC 9(02).
           05 WS-CENT-F                    PIC 9(02).
      *
       01  WS-HORARIO-P.
           05 WS-HORA-P                    PIC 9(02).
           05 FILLER                       PIC X(01) VALUE ":".
           05 WS-MIN-P                     PIC 9(02).
           05 FILLER                       PIC X(01) VALUE ":".
           05 WS-SEG-P                     PIC 9(02).
           05 FILLER                       PIC X(01) VALUE ":".
           05 WS-CENT-P                    PIC 9(02).
      *
       77  WS-TOTAL-CENT                   PIC 9(09).
      *
      *--------------------------------------------------------------*
       PROCEDURE                           DIVISION.
      *--------------------------------------------------------------*
       0000-ERIKEX07.
           PERFORM 1000-INICIALIZAR
           PERFORM 2000-PROCESSAR UNTIL FS-SIMUEMP = "10"
           PERFORM 3000-TERMINO
           STOP RUN
           .
      *
       1000-INICIALIZAR.
           ACCEPT WS-DATA-SISTEMA          FROM DATE
           MOVE CORRESPONDING WS-DATA-SISTEMA TO WS-DATA-FORMATADA
           ACCEPT WS-DATA-JULIANA          FROM DAY
           ACCEPT WS-DIA-SEMANA            FROM DAY-OF-WEEK
           ACCEPT WS-HORARIO-I             FROM TIME
           INITIALIZE WS-CTLIDOS
                      WS-CTGRAV
      *
           OPEN INPUT SIMUEMP
           IF FS-SIMUEMP NOT = ZEROS
              MOVE " ERRO DE ABERTURA DO ARQ SIMUEMP " TO WS-MSG
              MOVE FS-SIMUEMP              TO WS-FS
              GO                           TO 9999-ERRO
           END-IF
      *
           OPEN OUTPUT EMPAPROV
           IF FS-EMPAPROV NOT = ZEROS
              MOVE " ERRO DE ABERTURA DO ARQ EMPAPROV " TO WS-MSG
              MOVE FS-EMPAPROV             TO WS-FS
           GO                              TO 9999-ERRO
           END-IF
      *
           PERFORM 1500-LER-SIMUEMP
           .
      *
       1500-LER-SIMUEMP.
           READ SIMUEMP INTO WS-REG-SIMUEMP
           IF FS-SIMUEMP = ZEROS
              ADD 1                        TO WS-CTLIDOS
           ELSE
              IF FS-SIMUEMP NOT = "10"
                 MOVE " ERRO DE LEITURA DO ARQ SIMUEMP " TO WS-MSG
                 MOVE FS-SIMUEMP           TO WS-FS
                 GO                        TO 9999-ERRO
              END-IF
           END-IF
           .
      *
       2000-PROCESSAR.
           MOVE WS-NUM-SIMULACAO-S         TO WS-NUM-SIMULACAO-E
           MOVE WS-VAL-EMPRESTIMO-S        TO WS-VAL-EMPRESTIMO-E
           MOVE WS-TX-JUROS-S              TO WS-TX-JUROS-E
           MOVE WS-QTD-PARCELAS-S          TO WS-QTD-PARCELAS-E
           COMPUTE WS-VALOR-PARCELA = WS-VAL-EMPRESTIMO-S
                                             /
                                      WS-QTD-PARCELAS-S
           MOVE WS-VALOR-PARCELA           TO WS-VALOR-PARCELA-E
           COMPUTE WS-TOTAL-EMP = WS-VALOR-PARCELA * WS-QTD-PARCELAS-S
           MOVE WS-TOTAL-EMP               TO WS-TOTAL-EMP-E
      *
           WRITE REG-EMPAPROV              FROM WS-REG-EMPAPROV
           IF FS-EMPAPROV NOT = ZEROS
              MOVE " ERRO DE GRAVACAO DO ARQ EMPAPROV " TO WS-MSG
              MOVE FS-EMPAPROV             TO WS-FS
              GO                           TO 9999-ERRO
           END-IF
      *
           ADD 1                           TO WS-CTGRAV
           PERFORM 1500-LER-SIMUEMP
           .
      *
       3000-TERMINO.
      *----------------------CALCULO DO TEMPO---------------------
           ACCEPT WS-HORARIO-F             FROM TIME
           COMPUTE WS-TOTAL-CENT = ( WS-HORA-I * 360000 +
                                     WS-MIN-I  * 60000  +
                                     WS-SEG-I  * 100    +
                                     WS-CENT-I )
                                               -
                                   ( WS-HORA-F * 360000 +
                                     WS-MIN-F  * 60000  +
                                     WS-SEG-F  * 100    +
                                     WS-CENT-F )
           COMPUTE WS-HORA-P     = WS-TOTAL-CENT / 360000
           COMPUTE WS-TOTAL-CENT = WS-TOTAL-CENT - WS-HORA-P * 360000
           COMPUTE WS-MIN-P      = WS-TOTAL-CENT / 60000
           COMPUTE WS-TOTAL-CENT = WS-TOTAL-CENT - WS-MIN-P * 60000
           COMPUTE WS-SEG-P      = WS-TOTAL-CENT / 100
           COMPUTE WS-TOTAL-CENT = WS-TOTAL-CENT - WS-SEG-P * 100
           COMPUTE WS-CENT-P     = WS-TOTAL-CENT
      *----------------------FIM DO CALCULO-----------------------
           DISPLAY " "
           DISPLAY " ------------INFORMACOES GERAIS---------- "
           DISPLAY " TOTAL DE REGISTROS LIDOS....: " WS-CTLIDOS
           DISPLAY " TOTAL DE REGISTROS GRAVADOS.: " WS-CTGRAV
           DISPLAY " DATA DO PROCESSAMENTO.......: " WS-DATA-FORMATADA
           DISPLAY " DATA JULIANA................: " WS-DATA-JULIANA
           DISPLAY " DIA DA SEMANA...............: " WS-DIA-SEMANA
           DISPLAY " TEMPO TOTAL DO PROCESSAMENTO: " WS-HORARIO-P
           DISPLAY " ---------------------------------------- "
      *
           CLOSE SIMUEMP
           IF FS-SIMUEMP NOT = ZEROS
              MOVE " ERRO DE FECHAMENTO DO ARQ SIMUEMP " TO WS-MSG
              MOVE FS-SIMUEMP              TO WS-FS
              GO                           TO 9999-ERRO
           END-IF
      *
           CLOSE EMPAPROV
           IF FS-EMPAPROV NOT = ZEROS
              MOVE " ERRO DE FECHAMENTO DO ARQ EMPAPROV " TO WS-MSG
              MOVE FS-EMPAPROV             TO WS-FS
              GO                           TO 9999-ERRO
           END-IF
           .
      *
       9999-ERRO.
           DISPLAY " ---------------------------------------- "
           DISPLAY " ------------ROTINA DE ERRO ------------- "
           DISPLAY " MENSAGEM....: " WS-MSG
           DISPLAY " FILE STATUS.: " WS-FS
           DISPLAY " TERMINO ANORMAL DO JOB ERIKEX07 "
           STOP RUN
           .
